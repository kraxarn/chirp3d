plugins {
     id 'com.android.application'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.kraxarn.chirp3d"
    compileSdkVersion 35
    ndkVersion = "29.0.14206865"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-21", "-DANDROID_STL=c++_static"
                abiFilters "arm64-v8a"
            }
        }
    }
	signingConfigs {
		create("release") {
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
		}
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig = signingConfigs.getByName("release")
		}
	}
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn("externalNativeBuild${variant.name.capitalize()}")
    }
    if (!project.hasProperty('EXCLUDE_NATIVE_LIBS')) {
        sourceSets.main {
            jniLibs.srcDir 'libs'
        }
        externalNativeBuild {
			cmake {
				path "../../CMakeLists.txt"
			}
        }
    }
    lint {
        abortOnError = false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
}
